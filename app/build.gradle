plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.lv2.aap_fluidsynth"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName libs.versions.aap.lv2.get()

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    // See aap-core/androidaudioplugin/build.gradle to find out why it is required...
    ndkVersion libs.versions.ndk.get()

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildFeatures {
        prefab true
    }

    packagingOptions {
        if (enable_asan)
            jniLibs.useLegacyPackaging = true
    }

    aaptOptions {
        noCompress 'sf3'
    }
}

dependencies {
    implementation libs.aap.lv2
    implementation libs.aap.core
    implementation libs.aap.midi.device.service
    implementation libs.aap.ui.compose
    implementation libs.aap.ui.web
    implementation libs.startup.runtime

    testImplementation libs.junit
    androidTestImplementation libs.aap.testing
    androidTestImplementation libs.test.core
    androidTestImplementation libs.test.ext.junit
}

