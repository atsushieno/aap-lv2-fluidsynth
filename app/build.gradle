plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.lv2.aap_fluidsynth"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName "$aap_lv2_version"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    // See aap-core/androidaudioplugin/build.gradle to find out why it is required...
    ndkVersion "$ndk_version"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            version "3.22.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    buildFeatures {
        prefab true
    }

    // https://stackoverflow.com/questions/66343654/upgrading-to-jetpack-compose-1-0-0-alpha12-with-kotlin-1-4-30-error
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        if (enable_asan)
            jniLibs.useLegacyPackaging = true
    }

    aaptOptions {
        noCompress 'sf3'
    }
}

dependencies {
    implementation "org.androidaudioplugin:androidaudioplugin-lv2:$aap_lv2_version"
    implementation "org.androidaudioplugin:androidaudioplugin:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-ui-compose:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-midi-device-service:$aap_version"
    implementation "androidx.startup:startup-runtime:1.1.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "org.androidaudioplugin:androidaudioplugin-testing:$aap_version"
}

